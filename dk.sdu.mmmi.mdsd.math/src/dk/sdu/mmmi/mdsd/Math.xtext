grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

/*Model:
	(expressions+=MathExp)*
; */
MathExp:
	'var' name=ID '=' exp=Exp
;

Exp returns Expression:
	MultOrDiv ((
		'+' {Plus.left=current} |
		'-' {Minus.left=current} 		
	) right=MultOrDiv)*
;

MultOrDiv returns Expression:
	Primary ((
		'*' {Mult.left=current} |
		'/' {Div.left=current}
	) right=Primary)*

;

Primary returns Expression:
	Number | Parenthesis | VariableUse | Binding
;

VariableUse returns Expression:
	{Var} ref = [MathExp]
;

Binding returns Expression:
	{Let} 'let' id=ID '=' binding=Exp 'in' body=Exp 'end'
;

Parenthesis returns Expression:
	'(' Exp ')'
;

Number returns Expression:
	{Num} value=INT
;
